# -*- mode: Snakemake -*-
#
# Viral contig annotation:
# 	Rules for BLASTing against databases
#
# See Readme.md
import csv

from Bio import SearchIO
from xml.etree.ElementTree import ParseError

rule run_blastn:
    """Run BLASTn against a given database and write the results to XML."""    
    input:
        contigs=str(ASSEMBLY_FP/'{sample}_assembly'/'final-contigs.fa'),
    output:
        str(ANNOTATION_FP/'blastn'/'{db}'/'{contigs}'/'{sample}.xml')
    params:
        db=lambda wildcard: Blastdbs['nucl'][wildcard.db] 
    threads: 
        Cfg['blast']['threads']
    shell:
        """
        blastn \
        -query {input.contigs} \
        -db {params.db} \
        -outfmt 5 \
        -num_threads {threads} \
        -evalue 1e-10 \
        -max_target_seqs 1 \
        -out {output} \
        """

rule run_blastp:
    """Run BLASTp on translated genes against a target db and write to XML."""
    input:
        genes=str(ANNOTATION_FP/'genes'/'{orf_finder}'/'{sample}_genes_prot11.fa'),
        db=lambda wildcard: Blastdbs['prot'][wildcard.db]
    output:
        str(ANNOTATION_FP/'blastp'/'{db}'/'{orf_finder}'/'{sample}.xml')
    threads:
        Cfg['blast']['threads']
    shell:
        """
        blastp \
        -query {input.genes} \
        -db {input.db} \
        -outfmt 5 \
        -num_threads {threads} \
        -evalue 1e-10 \
        -max_target_seqs 1 \
        -out {output} \
        """

rule run_blastx:
    """Run BLASTx on untranslated genes against a target db and write to XML."""
    input:
        # output/virome/genes/mga/HUP3D03BALc_S92_genes_nucl.fa
        genes=str(ANNOTATION_FP/'genes'/'{orf_finder}'/'{sample}_genes_nucl.fa'),
        db=lambda wildcard: Blastdbs['prot'][wildcard.db]
    output:
        # output/virome/blastx/aclame/mga/HUP3D03balc_S92.xml
        str(ANNOTATION_FP/'blastx'/'{db}'/'{orf_finder}'/'{sample}.xml')
    threads:
        Cfg['blast']['threads']
    shell:
        """
        blastx \
        -query {input.genes} \
        -db {input.db} \
        -outfmt 5 \
        -num_threads {threads} \
        -evalue 1e-10 \
        -max_target_seqs 1 \
        -out {output} \
        """
        
rule blast_report:
    """Create a summary of results from a BLAST call."""
    input:
        expand(
            str(ANNOTATION_FP/'{{blast_prog}}'/'{{db}}'/'{{query}}'/'{sample}.xml'),
            sample=Samples.keys())
    output:
        str(ANNOTATION_FP/'{blast_prog}'/'{db}'/'{query}'/'report.tsv')
    run:
        with open(output[0], 'w') as out:
            writer = csv.DictWriter(
	        out,
	        fieldnames=['sample','query','hit'],
                delimiter='\t')
            writer.writeheader()
            list(writer.writerow(result) for result in blast_summary(input))


rule _test_blastx:
    input:
        expand(str(ANNOTATION_FP/'blastx'/'aclame'/'mga'/'{sample}.xml'),
               sample=Samples.keys())
    
rule _test_blast_report:
    input:
        str(ANNOTATION_FP/'blastx'/'aclame'/'mga'/'report.tsv')

rule _test_blast_card:
    input:
        str(ANNOTATION_FP/'blastx'/'card'/'mga'/'report.tsv')
